# Copyright 2018 The OLYMPUS Authors.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# OLYMPUS is Autograd and XLA

load("@bazel_skylib//lib:selects.bzl", "selects")
load("@bazel_skylib//rules:common_settings.bzl", "bool_flag", "string_flag")
load("@cuda_cudart//:version.bzl", cuda_major_version = "VERSION")
load("@local_config_cuda//cuda:build_defs.bzl", "if_cuda")
load("@local_config_rocm//rocm:build_defs.bzl", "if_rocm")
load("@rules_python//python:py_binary.bzl", "py_binary")
load(
    "@xla//third_party/py:py_import.bzl",
    "py_import",
)
load(
    "@xla//third_party/py:py_manylinux_compliance_test.bzl",
    "verify_manylinux_compliance_test",
)
load(
    "//olympuslib:olympus.bzl",
    "PLATFORM_TAGS_DICT",
    "compare_srcs_and_test_deps_test",
    "get_test_suite_list",
    "if_pypi_cuda_wheel_deps",
    "olympus_py_test",
    "olympus_wheel",
    "pytype_strict_library",
    "pytype_test",
    "wheel_sources",
)

licenses(["notice"])  # Apache 2

package(default_visibility = ["//visibility:public"])

exports_files(["wheel_size_test.py"])

genrule(
    name = "platform_tags_py",
    srcs = [],
    outs = ["platform_tags.py"],
    cmd = "echo 'PLATFORM_TAGS_DICT = %s' > $@;" % PLATFORM_TAGS_DICT,
)

pytype_strict_library(
    name = "build_utils",
    srcs = [
        "build_utils.py",
        ":platform_tags_py",
    ],
    deps = ["@xla//third_party/py:setup_py_nvidia_dependencies_util"],
)

# Platform configurations.

selects.config_setting_group(
    name = "macos",
    match_any = [
        "@platforms//os:osx",
        "@platforms//os:macos",
    ],
)

selects.config_setting_group(
    name = "arm64",
    match_any = [
        "@platforms//cpu:aarch64",
        "@platforms//cpu:arm64",
    ],
)

selects.config_setting_group(
    name = "macos_arm64",
    match_all = [
        ":arm64",
        ":macos",
    ],
)

selects.config_setting_group(
    name = "macos_x86_64",
    match_all = [
        "@platforms//cpu:x86_64",
        ":macos",
    ],
)

selects.config_setting_group(
    name = "win_amd64",
    match_all = [
        "@platforms//cpu:x86_64",
        "@platforms//os:windows",
    ],
)

selects.config_setting_group(
    name = "linux_x86_64",
    match_all = [
        "@platforms//cpu:x86_64",
        "@platforms//os:linux",
    ],
)

selects.config_setting_group(
    name = "linux_aarch64",
    match_all = [
        ":arm64",
        "@platforms//os:linux",
    ],
)

# Flags for the new wheel build rules.

string_flag(
    name = "olympuslib_git_hash",
    build_setting_default = "",
)

string_flag(
    name = "output_path",
    build_setting_default = "dist",
)

# Wheel targets.

# Olympuslib wheel targets.
py_binary(
    name = "build_wheel_tool",
    srcs = ["build_wheel.py"],
    main = "build_wheel.py",
    deps = [
        ":build_utils",
        "@bazel_tools//tools/python/runfiles",
        "@pypi//build",
        "@pypi//setuptools",
        "@pypi//wheel",
    ],
)

wheel_sources(
    name = "olympuslib_sources",
    data_srcs = [
        "//olympuslib",
        "//olympuslib:olympuslib_binaries",
        "//olympuslib:_olympus",
    ],
    hdr_srcs = [
        "@xla//xla/ffi/api:ffi",
    ],
    py_srcs = [
        "//olympuslib",
    ],
    static_srcs = [
        "//olympuslib:README.md",
        "LICENSE.txt",
        "//olympuslib:setup.py",
        "//olympuslib:xla_client.py",
    ],
    symlink_data_srcs = [
        "//olympuslib",
    ],
)

olympus_wheel(
    name = "olympuslib_wheel",
    no_abi = False,
    source_files = [":olympuslib_sources"],
    wheel_binary = ":build_wheel_tool",
    wheel_name = "olympuslib",
)

olympus_wheel(
    name = "olympuslib_wheel_editable",
    editable = True,
    source_files = [":olympuslib_sources"],
    wheel_binary = ":build_wheel_tool",
    wheel_name = "olympuslib",
)

# OLYMPUS plugin wheel targets.
pytype_strict_library(
    name = "version",
    srcs = ["//olympuslib:version"],
)

py_binary(
    name = "build_gpu_kernels_wheel_tool",
    srcs = ["build_gpu_kernels_wheel.py"],
    main = "build_gpu_kernels_wheel.py",
    deps = [
        ":build_utils",
        "@bazel_tools//tools/python/runfiles",
        "@pypi//build",
        "@pypi//setuptools",
        "@pypi//wheel",
    ],
)

wheel_sources(
    name = "olympus_plugin_sources",
    data_srcs = [
    ] + if_cuda([
        "//olympuslib/cuda:cuda_gpu_support",
        "@local_config_cuda//cuda:cuda-nvvm",
        "//olympuslib/cuda:cuda_plugin_extension",
        "//olympuslib/mosaic/gpu:mosaic_gpu",
    ]) + if_rocm([
        "//olympuslib/rocm:rocm_gpu_support",
        "//olympuslib/rocm:rocm_plugin_extension",
    ]),
    py_srcs = [":version"] + if_cuda([
        "//olympuslib/cuda:cuda_gpu_support",
        "//olympuslib/mosaic/gpu:mosaic_gpu",
    ]) + if_rocm([
        "//olympuslib/rocm:rocm_gpu_support",
    ]),
    static_srcs = [
        "LICENSE.txt",
    ] + if_cuda([
        "//olympus_plugins/cuda:plugin_pyproject.toml",
        "//olympus_plugins/cuda:plugin_setup.py",
    ]) + if_rocm([
        "//olympus_plugins/rocm:plugin_pyproject.toml",
        "//olympus_plugins/rocm:plugin_setup.py",
    ]),
)

olympus_wheel(
    name = "olympus_cuda12_plugin_wheel",
    enable_cuda = True,
    no_abi = False,
    # TODO(b/371217563) May use hermetic cuda version here.
    platform_version = "12",
    source_files = [":olympus_plugin_sources"],
    wheel_binary = ":build_gpu_kernels_wheel_tool",
    wheel_name = "olympus_cuda12_plugin",
)

olympus_wheel(
    name = "olympus_cuda13_plugin_wheel",
    enable_cuda = True,
    no_abi = False,
    # TODO(b/371217563) May use hermetic cuda version here.
    platform_version = "13",
    source_files = [":olympus_plugin_sources"],
    wheel_binary = ":build_gpu_kernels_wheel_tool",
    wheel_name = "olympus_cuda13_plugin",
)

olympus_wheel(
    name = "olympus_cuda12_plugin_wheel_editable",
    editable = True,
    enable_cuda = True,
    # TODO(b/371217563) May use hermetic cuda version here.
    platform_version = "12",
    source_files = [":olympus_plugin_sources"],
    wheel_binary = ":build_gpu_kernels_wheel_tool",
    wheel_name = "olympus_cuda12_plugin",
)

olympus_wheel(
    name = "olympus_cuda13_plugin_wheel_editable",
    editable = True,
    enable_cuda = True,
    # TODO(b/371217563) May use hermetic cuda version here.
    platform_version = "13",
    source_files = [":olympus_plugin_sources"],
    wheel_binary = ":build_gpu_kernels_wheel_tool",
    wheel_name = "olympus_cuda13_plugin",
)

olympus_wheel(
    name = "olympus_rocm_plugin_wheel",
    enable_rocm = True,
    no_abi = False,
    platform_version = "60",
    source_files = [":olympus_plugin_sources"],
    wheel_binary = ":build_gpu_kernels_wheel_tool",
    wheel_name = "olympus_rocm60_plugin",
)

olympus_wheel(
    name = "olympus_rocm_plugin_wheel_editable",
    editable = True,
    enable_rocm = True,
    platform_version = "60",
    source_files = [":olympus_plugin_sources"],
    wheel_binary = ":build_gpu_kernels_wheel_tool",
    wheel_name = "olympus_rocm60_plugin",
)

# OLYMPUS PJRT wheel targets.

py_binary(
    name = "build_gpu_plugin_wheel_tool",
    srcs = ["build_gpu_plugin_wheel.py"],
    main = "build_gpu_plugin_wheel.py",
    deps = [
        ":build_utils",
        "@bazel_tools//tools/python/runfiles",
        "@pypi//build",
        "@pypi//setuptools",
        "@pypi//wheel",
    ],
)

wheel_sources(
    name = "olympus_pjrt_sources",
    data_srcs = if_cuda([
        "//olympus_plugins/cuda:cuda_plugin",
        "//olympuslib/cuda:cuda_gpu_support",
        "@local_config_cuda//cuda:cuda-nvvm",
    ]) + if_rocm([
        "//olympus_plugins/rocm:rocm_plugin",
        "//olympuslib/rocm:rocm_gpu_support",
    ]),
    py_srcs = [
        ":version",
    ] + if_cuda([
        "//olympuslib/cuda:cuda_gpu_support",
    ]) + if_rocm([
        "//olympuslib/rocm:rocm_gpu_support",
    ]),
    static_srcs = [
        "LICENSE.txt",
    ] + if_cuda([
        "//olympus_plugins/cuda:pyproject.toml",
        "//olympus_plugins/cuda:setup.py",
        "//olympus_plugins/cuda:__init__.py",
    ]) + if_rocm([
        "//olympus_plugins/rocm:pyproject.toml",
        "//olympus_plugins/rocm:setup.py",
        "//olympus_plugins/rocm:__init__.py",
    ]),
)

olympus_wheel(
    name = "olympus_cuda12_pjrt_wheel",
    enable_cuda = True,
    no_abi = True,
    # TODO(b/371217563) May use hermetic cuda version here.
    platform_version = "12",
    source_files = [":olympus_pjrt_sources"],
    wheel_binary = ":build_gpu_plugin_wheel_tool",
    wheel_name = "olympus_cuda12_pjrt",
)

olympus_wheel(
    name = "olympus_cuda13_pjrt_wheel",
    enable_cuda = True,
    no_abi = True,
    # TODO(b/371217563) May use hermetic cuda version here.
    platform_version = "13",
    source_files = [":olympus_pjrt_sources"],
    wheel_binary = ":build_gpu_plugin_wheel_tool",
    wheel_name = "olympus_cuda13_pjrt",
)

olympus_wheel(
    name = "olympus_cuda12_pjrt_wheel_editable",
    editable = True,
    enable_cuda = True,
    # TODO(b/371217563) May use hermetic cuda version here.
    platform_version = "12",
    source_files = [":olympus_pjrt_sources"],
    wheel_binary = ":build_gpu_plugin_wheel_tool",
    wheel_name = "olympus_cuda12_pjrt",
)

olympus_wheel(
    name = "olympus_cuda13_pjrt_wheel_editable",
    editable = True,
    enable_cuda = True,
    # TODO(b/371217563) May use hermetic cuda version here.
    platform_version = "13",
    source_files = [":olympus_pjrt_sources"],
    wheel_binary = ":build_gpu_plugin_wheel_tool",
    wheel_name = "olympus_cuda13_pjrt",
)

olympus_wheel(
    name = "olympus_rocm_pjrt_wheel",
    enable_rocm = True,
    no_abi = True,
    platform_version = "60",
    source_files = [":olympus_pjrt_sources"],
    wheel_binary = ":build_gpu_plugin_wheel_tool",
    wheel_name = "olympus_rocm60_pjrt",
)

olympus_wheel(
    name = "olympus_rocm_pjrt_wheel_editable",
    editable = True,
    enable_rocm = True,
    platform_version = "60",
    source_files = [":olympus_pjrt_sources"],
    wheel_binary = ":build_gpu_plugin_wheel_tool",
    wheel_name = "olympus_rocm60_pjrt",
)

# Py_import targets.
cuda_suffix = "_cu12" if cuda_major_version == "12" else ""

filegroup(
    name = "nvidia_wheel_deps",
    srcs = [
        "@pypi_nvidia_cublas{cuda}//:pkg".format(cuda = cuda_suffix),
        "@pypi_nvidia_cuda_cupti{cuda}//:pkg".format(cuda = cuda_suffix),
        "@pypi_nvidia_cuda_nvcc{cuda}//:pkg".format(cuda = cuda_suffix),
        "@pypi_nvidia_cuda_nvrtc{cuda}//:pkg".format(cuda = cuda_suffix),
        "@pypi_nvidia_cuda_runtime{cuda}//:pkg".format(cuda = cuda_suffix),
        "@pypi_nvidia_cudnn_cu{cuda}//:pkg".format(cuda = cuda_major_version),
        "@pypi_nvidia_cufft{cuda}//:pkg".format(cuda = cuda_suffix),
        "@pypi_nvidia_cusolver{cuda}//:pkg".format(cuda = cuda_suffix),
        "@pypi_nvidia_cusparse{cuda}//:pkg".format(cuda = cuda_suffix),
        "@pypi_nvidia_nccl_cu{cuda}//:pkg".format(cuda = cuda_major_version),
        "@pypi_nvidia_nvjitlink{cuda}//:pkg".format(cuda = cuda_suffix),
        "@pypi_nvidia_nvshmem_cu{cuda}//:pkg".format(cuda = cuda_major_version),
    ] + ([
        "@pypi_nvidia_nvvm{cuda}//:pkg".format(cuda = cuda_suffix),
        "@pypi_nvidia_cuda_crt{cuda}//:pkg".format(cuda = cuda_suffix),
    ] if cuda_major_version == "13" else []),
)

# The flag configures whether to add the pypi NVIDIA CUDA deps to py_import.
bool_flag(
    name = "add_pypi_cuda_wheel_deps",
    build_setting_default = True,
)

config_setting(
    name = "pypi_cuda_wheel_deps",
    flag_values = {
        ":add_pypi_cuda_wheel_deps": "True",
        "@local_config_cuda//:enable_cuda": "True",
    },
)

py_import(
    name = "olympuslib_py_import",
    wheel = ":olympuslib_wheel",
)

py_import(
    name = "olympus_cuda_plugin_py_import",
    wheel = ":olympus_cuda{cuda}_plugin_wheel".format(cuda = cuda_major_version),
    wheel_deps = if_pypi_cuda_wheel_deps([":nvidia_wheel_deps"]),
)

py_import(
    name = "olympus_cuda_pjrt_py_import",
    wheel = ":olympus_cuda{cuda}_pjrt_wheel".format(cuda = cuda_major_version),
    wheel_deps = if_pypi_cuda_wheel_deps([":nvidia_wheel_deps"]),
)

# The targets below are used for GPU tests with `--//olympus:build_olympuslib=false`.
py_import(
    name = "pypi_olympus_cuda_plugin_with_cuda_deps",
    wheel = "@pypi_olympus_cuda{cuda}_plugin//:whl".format(cuda = cuda_major_version),
    wheel_deps = if_pypi_cuda_wheel_deps([":nvidia_wheel_deps"]),
)

py_import(
    name = "pypi_olympus_cuda_pjrt_with_cuda_deps",
    wheel = "@pypi_olympus_cuda{cuda}_pjrt//:whl".format(cuda = cuda_major_version),
    wheel_deps = if_pypi_cuda_wheel_deps([":nvidia_wheel_deps"]),
)

# Mosaic GPU

py_binary(
    name = "build_mosaic_wheel_tool",
    srcs = ["build_mosaic_wheel.py"],
    main = "build_mosaic_wheel.py",
    deps = [
        ":build_utils",
        "@bazel_tools//tools/python/runfiles",
        "@pypi//build",
        "@pypi//setuptools",
        "@pypi//wheel",
    ],
)

wheel_sources(
    name = "mosaic_sources",
    static_srcs = [
        "LICENSE.txt",
        "//olympuslib/mosaic/gpu/wheel:mosaic_gpu.so",
        "//olympuslib/mosaic/gpu/wheel:setup.py",
        "//olympuslib/mosaic/gpu/wheel:__init__.py",
        "//olympuslib:version",
    ],
)

olympus_wheel(
    name = "mosaic_gpu_wheel_cuda12",
    enable_cuda = True,
    no_abi = True,
    platform_version = "12",
    source_files = [":mosaic_sources"],
    wheel_binary = ":build_mosaic_wheel_tool",
    wheel_name = "mosaic_gpu_cuda12",
)

olympus_wheel(
    name = "mosaic_gpu_wheel_cuda13",
    enable_cuda = True,
    no_abi = True,
    platform_version = "13",
    source_files = [":mosaic_sources"],
    wheel_binary = ":build_mosaic_wheel_tool",
    wheel_name = "mosaic_gpu_cuda13",
)

# Wheel tests.

AARCH64_MANYLINUX_TAG = "_".join(PLATFORM_TAGS_DICT[("Linux", "aarch64")])

PPC64LE_MANYLINUX_TAG = "_".join(PLATFORM_TAGS_DICT[("Linux", "ppc64le")])

X86_64_MANYLINUX_TAG = "_".join(PLATFORM_TAGS_DICT[("Linux", "x86_64")])

verify_manylinux_compliance_test(
    name = "olympuslib_manylinux_compliance_test",
    aarch64_compliance_tag = AARCH64_MANYLINUX_TAG,
    ppc64le_compliance_tag = PPC64LE_MANYLINUX_TAG,
    test_tags = [
        "manual",
    ],
    wheel = ":olympuslib_wheel",
    x86_64_compliance_tag = X86_64_MANYLINUX_TAG,
)

verify_manylinux_compliance_test(
    name = "olympus_cuda_plugin_manylinux_compliance_test",
    aarch64_compliance_tag = AARCH64_MANYLINUX_TAG,
    ppc64le_compliance_tag = PPC64LE_MANYLINUX_TAG,
    test_tags = [
        "manual",
    ],
    wheel = ":olympus_cuda{cuda}_plugin_wheel".format(cuda = cuda_major_version),
    x86_64_compliance_tag = X86_64_MANYLINUX_TAG,
)

verify_manylinux_compliance_test(
    name = "olympus_cuda_pjrt_manylinux_compliance_test",
    aarch64_compliance_tag = AARCH64_MANYLINUX_TAG,
    ppc64le_compliance_tag = PPC64LE_MANYLINUX_TAG,
    test_tags = [
        "manual",
    ],
    wheel = ":olympus_cuda{cuda}_pjrt_wheel".format(cuda = cuda_major_version),
    x86_64_compliance_tag = X86_64_MANYLINUX_TAG,
)

verify_manylinux_compliance_test(
    name = "mosaic_gpu_manylinux_compliance_test",
    aarch64_compliance_tag = AARCH64_MANYLINUX_TAG,
    ppc64le_compliance_tag = PPC64LE_MANYLINUX_TAG,
    test_tags = [
        "manual",
    ],
    wheel = ":mosaic_gpu_wheel_cuda{cuda}".format(cuda = cuda_major_version),
    x86_64_compliance_tag = X86_64_MANYLINUX_TAG,
)

pytype_test(
    name = "olympuslib_wheel_size_test",
    srcs = [":wheel_size_test.py"],
    args = [
        "--wheel-path=$(location :olympuslib_wheel)",
        "--max-size-mib=110",
    ],
    data = [":olympuslib_wheel"],
    main = "wheel_size_test.py",
    tags = [
        "manual",
        "notap",
    ],
)

pytype_test(
    name = "olympus_cuda_plugin_wheel_size_test",
    srcs = [":wheel_size_test.py"],
    args = [
        "--wheel-path=$(location :olympus_cuda{cuda}_plugin_wheel)".format(cuda = cuda_major_version),
        "--max-size-mib=20",
    ],
    data = [":olympus_cuda{cuda}_plugin_wheel".format(cuda = cuda_major_version)],
    main = "wheel_size_test.py",
    tags = [
        "manual",
        "notap",
    ],
)

pytype_test(
    name = "olympus_cuda_pjrt_wheel_size_test",
    srcs = [":wheel_size_test.py"],
    args = [
        "--wheel-path=$(location :olympus_cuda{cuda}_pjrt_wheel)".format(cuda = cuda_major_version),
        "--max-size-mib=150",
    ],
    data = [":olympus_cuda{cuda}_pjrt_wheel".format(cuda = cuda_major_version)],
    main = "wheel_size_test.py",
    tags = [
        "manual",
        "notap",
    ],
)

pytype_test(
    name = "mosaic_gpu_wheel_size_test",
    srcs = [":wheel_size_test.py"],
    args = [
        "--wheel-path=$(location :mosaic_gpu_wheel_cuda{cuda})".format(cuda = cuda_major_version),
        "--max-size-mib=40",
    ],
    data = [":mosaic_gpu_wheel_cuda{cuda}".format(cuda = cuda_major_version)],
    main = "wheel_size_test.py",
    tags = [
        "manual",
        "notap",
    ],
)

_IGNORED_INIT_PY_FILES = [
    # This file is copied as `__init__.py` in the root wheel folder.
    "olympuslib/__init__.py",
    # These files do not exist in LLVM/MLIR sources.
    "olympuslib/mlir/__init__.py",
    "olympuslib/mlir/dialects/__init__.py",
    "olympuslib/mlir/extras/__init__.py",
    # These files do not exist in OLYMPUS sources.
    "olympuslib/mosaic/dialect/gpu/__init__.py",
    "olympuslib/mosaic/python/__init__.py",
]

compare_srcs_and_test_deps_test(
    name = "check_cpu_wheel_sources_test",
    srcs = [
        ":olympuslib_sources",
        "//:olympus_sources",
        "//:wheel_additives",
    ],
    ignored_init_py_files = _IGNORED_INIT_PY_FILES,
    root_package_names = [
        "olympus",
        "olympuslib",
    ],
    tags = [
        "manual",
        "notap",
    ],
    tests = [
        "//olympus/experimental/olympus2tf/tests:olympus2tf_test_cpu",
    ] + get_test_suite_list(
        backends = ["cpu"],
        paths = [
            "olympus/experimental/olympus2tf/tests/multiprocess",
            "tests",
            "tests/mosaic",
            "tests/multiprocess",
            "tests/pallas",
        ],
    ),
)

compare_srcs_and_test_deps_test(
    name = "check_gpu_wheel_sources_test",
    srcs = [
        ":olympus_pjrt_sources",
        ":olympus_plugin_sources",
        ":olympuslib_sources",
        "//:olympus_sources",
        "//:wheel_additives",
    ],
    ignored_init_py_files = _IGNORED_INIT_PY_FILES,
    root_package_names = [
        "olympus",
        "olympuslib",
    ],
    tags = [
        "manual",
        "notap",
    ],
    tests = get_test_suite_list(
        backends = ["gpu"],
        paths = [
            "olympus/experimental/olympus2tf/tests/multiprocess",
            "tests",
            "tests/mosaic",
            "tests/multiprocess",
            "tests/pallas",
        ],
    ),
)

compare_srcs_and_test_deps_test(
    name = "check_tpu_wheel_sources_test",
    srcs = [
        ":olympuslib_sources",
        "//:olympus_sources",
        "//:wheel_additives",
    ],
    ignored_init_py_files = _IGNORED_INIT_PY_FILES,
    root_package_names = [
        "olympus",
        "olympuslib",
    ],
    tags = [
        "manual",
        "notap",
    ],
    tests = get_test_suite_list(
        backends = ["tpu"],
        paths = [
            "tests",
            "tests/multiprocess",
            "tests/pallas",
        ],
    ),
)
